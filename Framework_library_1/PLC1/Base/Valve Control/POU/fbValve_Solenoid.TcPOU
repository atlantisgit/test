<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="fbValve_Solenoid" Id="{4590884d-7356-4537-bc1e-3a60dfcf47a8}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK fbValve_Solenoid
VAR_INPUT
	tInTimeout: TIME:=T#10S;		//The amount of time before an error condition triggers an error
	iInMode:	INT;		//system mode 1=Auto, 2=Manual, 10=Independent
	bInEstop:	BOOL;		//Emergency stop input 1=Estop
	bInSignalHome:	BOOL;	//Home position feedback
	bInSignalWork:	BOOL;	//Work position feedback
	bInEnable:		BOOL;	//Valve interlock. The valve is allowed to operate if this condition has been met
	bInCommandWork: BOOL;	//Move to work position in automatic mode
	bInResetError:	BOOL;	//If an error condition exists, the error must be fixed first, then set this bit high to reset the internal error
	bInSimulate:	BOOL;	//Enables software simulation of device.
END_VAR
VAR_OUTPUT
	bOutCommandHome: BOOL;	//Output for home position
	bOutCommandWork: BOOL;	//Output for work position
	bOutActiveHome: BOOL;	//Valve is actively in home position
	bOutActiveWork: BOOL;	//Valve is actively in work position
	bOutAuto: BOOL;			//This output indicates whether the block is in auto mode
	bOutError: BOOL;		//This output indicates whether there's an error condition in the cylinder
	ERROR_Valve:stError_Valve;
END_VAR

VAR_IN_OUT
	HMI_ValveControl:stHMI_ValveControl;
END_VAR

VAR
	tonTimeOut:TON;
	iErrorCode:INT;
	bNewMode:BOOL;	 (*new mode selected*)
	bAutoMode:BOOL;	 (*Automode active*)
	bManualMode:BOOL; (*Manual Mode active*)
	iLastMode:INT;	(*last mode code*)
	bEnableHome:BOOL;	 (*home position enabled*)
	bEnableWork:BOOL;	 (*work position enabled*)
	bPB_Home:BOOL;		 (*PB Move to home in manual mode*)
	bPB_Work:BOOL;		 (*PB Move to work in manual mode*)
	bPB_ResetError:BOOL; (*PB Reset error*)
	bReset: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[

(*****************************INPUTS*************************************************)
(*Read HMI input button*)

fcHMIBit(bInToggle:=0 , bInLatch:=0 , bHMI:=HMI_ValveControl.bPB_Home , bPLC:=bPB_Home );
fcHMIBit(bInToggle:=0 , bInLatch:=0 , bHMI:=HMI_ValveControl.bPB_Work , bPLC:=bPB_Work );
fcHMIBit(bInToggle:=0 , bInLatch:=0 , bHMI:=HMI_ValveControl.bPB_ResetError , bPLC:=bPB_ResetError );
(*update the signal home bit*)
HMI_ValveControl.bSignalHome:=bInSignalHome;

(*update the signal work bit*)
HMI_ValveControl.bSignalWork:=bInSignalWork;

(**************************CONTROL LOGIC************************************************)
(*Clear all errors*)
IF (bInResetError OR bPB_ResetError) AND HMI_ValveControl.bPB_ResetError THEN
	bReset:=1;
ELSE 
	bReset:=0;
END_IF


(*override HMI mode if not in Independant mode*)
IF iInMode<>10 THEN
	HMI_ValveControl.iMode:=iInMode;
END_IF

(*detect change in mode*)
bNewMode:=HMI_ValveControl.iMode<>iLastMode;

(*keep track of last mode*)
iLastMode:=HMI_ValveControl.iMode;

(*Determine if auto or manual mode Active*)
bManualMode:=NOT(bInEstop) AND NOT(bNewMode) AND (HMI_ValveControl.iMode=2);
bAutoMode:=NOT(bInEstop) AND NOT(bNewMode) AND (HMI_ValveControl.iMode=1);

(*Enable home and work positions*)
bEnableWork:=bEnableHome:= NOT(HMI_ValveControl.bError) AND (bManualMode OR bAutoMode);

HMI_ValveControl.bPBEN_Home:=HMI_ValveControl.bPBEN_Work:=bManualMode AND NOT(HMI_ValveControl.bError);

(*enable error reset button if error present*)
HMI_ValveControl.bPBEN_ResetError:=HMI_ValveControl.bPB_ResetError;

(*Command Home position*)
IF (bManualMode AND bPB_Home AND  NOT(bPB_Work) AND HMI_ValveControl.bPBEN_Home) OR
	(bAutoMode AND NOT(bInCommandWork) AND bEnableHome) THEN
	HMI_ValveControl.bHomeOn:=1;
	HMI_ValveControl.bWorkOn:=0;
END_IF

(*command work position*)
IF (bManualMode AND bPB_Work AND  NOT(bPB_Home) AND HMI_ValveControl.bPBEN_Work) OR
	(bAutoMode AND bInCommandWork AND bEnableWork) THEN
	HMI_ValveControl.bWorkOn:=1;
	HMI_ValveControl.bHomeOn:=0;
END_IF

(*reset work if not enabled*)
IF bAutoMode AND NOT(bEnableWork) THEN
	HMI_ValveControl.bWorkOn:=0;
END_IF

(*reset home if not enabled*)
IF bAutoMode AND NOT(bEnableHome) THEN
	HMI_ValveControl.bHomeOn:=0;
END_IF

(*if Not in auto or manual reset both*)
IF NOT(bAutoMode OR bManualMode) THEN
	HMI_ValveControl.bHomeOn:=0;
	HMI_ValveControl.bWorkOn:=0;
END_IF

(************************************ERRORS*************************************************************)
(*set timeout errors*)
tonTimeOut(IN:=HMI_ValveControl.bHomeOn OR HMI_ValveControl.bWorkOn , PT:= , Q=> , ET=> );

(*Error: Work Feedback not active*)
ERROR_Valve.NoWorkFeedback:=NOT(bInEstop) AND tonTimeOut.Q AND HMI_ValveControl.bWorkOn AND NOT(HMI_ValveControl.bSignalWork);

(*Error: Home feedback not active*)
ERROR_Valve.NoHomeFeedback:=NOT(bInEstop) AND tonTimeOut.Q AND HMI_ValveControl.bHomeOn AND NOT(HMI_ValveControl.bSignalHome);

(*Error: Home position still active*)
ERROR_Valve.HomeFeedbackStillActive:=NOT(bInEstop) AND tonTimeOut.Q AND HMI_ValveControl.bWorkOn AND HMI_ValveControl.bSignalHome;

(*Error: Work position still active*)
ERROR_Valve.WorkFeedbackStillActive:=NOT(bInEstop) AND tonTimeOut.Q AND HMI_ValveControl.bHomeOn AND HMI_ValveControl.bSignalWork;

HMI_ValveControl.iErrorCode.0:=ERROR_Valve.NoWorkFeedback;
HMI_ValveControl.iErrorCode.1:=ERROR_Valve.NoHomeFeedback;
HMI_ValveControl.iErrorCode.2:=ERROR_Valve.HomeFeedbackStillActive;
HMI_ValveControl.iErrorCode.3:=ERROR_Valve.WorkFeedbackStillActive;

HMI_ValveControl.bError:=HMI_ValveControl.iErrorCode<>0;

(*Interlock*)
HMI_ValveControl.bInterlock:=bInEnable;

(************************************OUTPUTS**********************************************************************************)
(*Output Home*)
bOutCommandHome:=HMI_ValveControl.bHomeOn;

(*Output Work*)
bOutCommandWork:=HMI_ValveControl.bWorkOn;

(*Output Auto*)
bOutAuto:=(HMI_ValveControl.iMode=1);

(*Output error*)
bOutError:=HMI_ValveControl.bError;

]]></ST>
    </Implementation>
    <LineIds Name="fbValve_Solenoid">
      <LineId Id="240" Count="1" />
      <LineId Id="219" Count="1" />
      <LineId Id="281" Count="1" />
      <LineId Id="226" Count="0" />
      <LineId Id="283" Count="0" />
      <LineId Id="225" Count="0" />
      <LineId Id="231" Count="0" />
      <LineId Id="228" Count="0" />
      <LineId Id="227" Count="0" />
      <LineId Id="230" Count="0" />
      <LineId Id="229" Count="0" />
      <LineId Id="221" Count="0" />
      <LineId Id="234" Count="0" />
      <LineId Id="232" Count="0" />
      <LineId Id="235" Count="1" />
      <LineId Id="238" Count="1" />
      <LineId Id="145" Count="5" />
      <LineId Id="155" Count="0" />
      <LineId Id="153" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="158" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="159" Count="1" />
      <LineId Id="152" Count="0" />
      <LineId Id="167" Count="1" />
      <LineId Id="164" Count="1" />
      <LineId Id="171" Count="0" />
      <LineId Id="177" Count="0" />
      <LineId Id="173" Count="0" />
      <LineId Id="175" Count="0" />
      <LineId Id="166" Count="0" />
      <LineId Id="178" Count="3" />
      <LineId Id="187" Count="0" />
      <LineId Id="189" Count="1" />
      <LineId Id="188" Count="0" />
      <LineId Id="192" Count="0" />
      <LineId Id="191" Count="0" />
      <LineId Id="194" Count="3" />
      <LineId Id="193" Count="0" />
      <LineId Id="199" Count="0" />
      <LineId Id="198" Count="0" />
      <LineId Id="200" Count="0" />
      <LineId Id="204" Count="1" />
      <LineId Id="201" Count="0" />
      <LineId Id="206" Count="2" />
      <LineId Id="202" Count="0" />
      <LineId Id="214" Count="0" />
      <LineId Id="209" Count="0" />
      <LineId Id="203" Count="0" />
      <LineId Id="210" Count="0" />
      <LineId Id="212" Count="0" />
      <LineId Id="211" Count="0" />
      <LineId Id="242" Count="0" />
      <LineId Id="217" Count="0" />
      <LineId Id="215" Count="1" />
      <LineId Id="243" Count="2" />
      <LineId Id="249" Count="1" />
      <LineId Id="247" Count="0" />
      <LineId Id="251" Count="0" />
      <LineId Id="246" Count="0" />
      <LineId Id="248" Count="0" />
      <LineId Id="253" Count="1" />
      <LineId Id="252" Count="0" />
      <LineId Id="274" Count="1" />
      <LineId Id="277" Count="0" />
      <LineId Id="257" Count="0" />
      <LineId Id="255" Count="0" />
      <LineId Id="279" Count="0" />
      <LineId Id="278" Count="0" />
      <LineId Id="280" Count="0" />
      <LineId Id="256" Count="0" />
      <LineId Id="258" Count="1" />
      <LineId Id="213" Count="0" />
      <LineId Id="262" Count="1" />
      <LineId Id="266" Count="0" />
      <LineId Id="265" Count="0" />
      <LineId Id="267" Count="0" />
      <LineId Id="270" Count="0" />
      <LineId Id="269" Count="0" />
      <LineId Id="271" Count="1" />
      <LineId Id="47" Count="0" />
      <LineId Id="273" Count="0" />
      <LineId Id="105" Count="0" />
      <LineId Id="218" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>